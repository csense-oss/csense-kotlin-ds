buildscript {

    ext.kotlinVersion = "1.3.72"
    ext.dokkaVersion = "0.10.1"
    ext.coroutinesVersion = "1.3.6"
    ext.junit5Version = "5.5.2"
    ext.useCsenseVersion = "0.0.34"
    ext.useCsenseTestsVersion = "0.0.27"

    //library settings
    ext.csenseUserOrg = "csense-oss"
    ext.csenseRepoName = "maven"
    ext.csenseVersionName = "0.0.25"
    ext.csenseGroupId = "csense.kotlin"
    ext.csenseArtifactId = "csense-kotlin-ds"
    ext.csenseGithub = "csense-oss/csense-kotlin-ds"
    ext.csenseLicense = "MIT"
    ext.readme = "readme.md"
    ext.csenseWebsite = "https://github.com/csense-oss/csense-kotlin-ds"
    ext.csenseGit = "https://github.com/csense-oss/csense-kotlin-ds.git"
    //end library settings
}


plugins {
    id 'org.jetbrains.kotlin.multiplatform' version "1.3.72"
    id 'org.jetbrains.dokka' version '0.10.1'
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}


group project.csenseGroupId
version project.csenseVersionName

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/csense-oss/maven" }
}

kotlin {
    jvm()
    js {
        browser {
        }
        nodejs {
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutinesVersion"
                implementation "csense.kotlin:csense-kotlin-metadata:$useCsenseVersion"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
                implementation "csense.kotlin:csense-kotlin-tests:$useCsenseTestsVersion"
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
                implementation "csense.kotlin:csense-kotlin-jvm:$useCsenseVersion"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test-junit')
                implementation "org.jetbrains.kotlin:kotlin-reflect"
                implementation "csense.kotlin:csense-kotlin-tests-jvm:$useCsenseTestsVersion"
            }

        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$coroutinesVersion"
                implementation "csense.kotlin:csense-kotlin-js:$useCsenseVersion"
            }
        }
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
                implementation "csense.kotlin:csense-kotlin-tests-js:$useCsenseTestsVersion"
            }
        }
    }
}


kotlin.sourceSets.all {
    languageSettings.progressiveMode = true
}

dokka {
    multiplatform {
        global {
            perPackageOption {
                prefix = "csense.kotlin.ds"
                suppress = true
            }
        }
        js {}
        jvm {}
    }
}

bintray {
    Properties properties = new Properties()
    try {
        properties.load(project.rootProject.file('local.properties')?.newDataInputStream())
        user = properties.getProperty('bintrayUser')
        key = properties.getProperty('bintrayApiKey')
    } catch (Exception ignored) {
        print "could not read local.properties"
    }
    publications = publishing.publications.getNames()
    override = true
    publish = false
    pkg {
        repo = project.csenseRepoName
        userOrg = project.csenseUserOrg
        vcsUrl = project.csenseGit
        licenses = project.csenseLicense
        name = project.csenseArtifactId
    }
}


task uploadProd(dependsOn: ["assemble", "allTests", "dokka", "javadocJar", "bintrayUpload"])



task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/dokkaJavadoc"
}

final def javaDocJarTask = task("javadocJar", type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set('javadoc')
    from "${project.buildDir}/dokkaJavadoc"
}


publishing {
    publications {
        jvm {
            artifact(javaDocJarTask)
        }
    }
}
